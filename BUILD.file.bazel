package(default_visibility = ["//visibility:public"])

DIRS = ["files/"] + [
    "%s/**/" % file
    for file in glob(
        ["files/*"],
        exclude_directories = 0,
    )
    if file != "files/node_modules" and
       file != "files/.node_modules" and
       file != "files/.git" and
       file != "files/.bazel_cache" and
       not file.startswith("files/bazel-")
]

filegroup(
    name = "buildifier_files",
    srcs = glob(
        [
            dir + pattern
            for dir in DIRS
            for pattern in [
                "*.bazel",
                "*.bzl",
            ]
        ],
    ),
)

filegroup(
    name = "eslint_files",
    srcs = glob(
        [
            dir + pattern
            for dir in DIRS
            for pattern in [
                "*.js",
                "*.ts",
            ]
        ],
        ["**/dist/**"],
    ),
)

filegroup(
    name = "prettier_files",
    srcs = glob(
        [
            dir + pattern
            for dir in DIRS
            for pattern in [
                "*.html",
                "*.js",
                "*.json",
                "*.md",
                "*.ts",
                "*.yml",
            ]
        ],
        ["**/dist/**"],
    ),
)

filegroup(
    name = "common",
    srcs = glob(
        [
            dir + pattern
            for dir in DIRS
            for pattern in ["*"]
        ],
        [
            "**/test/**",
        ],
    ) + glob(["files/test/**"]),
)
